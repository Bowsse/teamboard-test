*** Settings ***
Library           ExtendedSelenium2Library
Library           Collections
Library           String

*** Variables ***
${Browser}        firefox
${DELAY}          0.2
${Username}       guidemaker@test.com
${Password}       guidemaker12
${NewPassword}    guidemaker21
#${Protocol}      https://
#${SERVER}        sut-cb.n4sjamk.org
${Protocol}       http://
${SERVER}         localhost:8000
${MainUrl}        ${Protocol}${SERVER}
${LoginUrl}       ${Protocol}${SERVER}/login
${BoardUrl}       ${Protocol}${SERVER}/boards
${RegUrl}         ${Protocol}${SERVER}/register
${LoginTitle}     Contriboard

*** Keywords ***
Open Browser To Login Page
    Open Browser    ${LoginUrl}    ${Browser}
    Set Window Size    1920    1200
    Maximize Browser Window
    Set Selenium Speed    ${DELAY}
    Location Should Be    ${LoginUrl}
    Title Should Be    ${LoginTitle}
    Wait Until Page Contains Element    //form[@class='form']    timeout=2    error=Login Form Not Found
    Capture Page Screenshot    login.png

Register User
    Element Text Should Be    //button[@class='btn-secondary']    Register    error=Register button missing
    Click Button    //button[@class='btn-secondary']
    Wait Until Page Contains Element    //form[@class='form']    timeout=2    error=Form not found
    Location Should Be    ${RegUrl}
    Capture Page Screenshot    register.png
    Input Username    ${Username}
    Input User Password    ${Password}
    Input User Password Again    ${Password}
    ${check}    Get Element Attribute    //input[@class='btn-primary']@value
    Should Be Equal    ${check}    Register    error=Register button missing
    Submit Credentials
    Wait Until Page Contains Element    //div[@class='view view-workspace']    timeout=2    error=Board list Not Found
    Location Should Be    ${BoardUrl}
    Capture Page Screenshot    boardview.png

Input Username
    [Arguments]    ${username}
    Input Text    //input[@type='email']    ${username}

Input User Password
    [Arguments]    ${password}
    Input Password    //input[@type='password']    ${password}

Input User Password Again
    [Arguments]    ${password}
    Input Password    //input[@name='passwordAgain']    ${password}

Submit Credentials
    Click Button    //input[@class='btn-primary']

Create Board
    Click Element    //span[@class='fa fa-fw fa-plus']

Open Board Edit Dialog
    [Arguments]    ${min}=2    ${max}=4
    Get Edit Xpath    ${min}    ${max}
    Click Element    ${Expath}
    Wait Until Page Contains Element    //form[@class='dialog dialog-edit-board']    timeout=2    error=Board Edit Dialog Not Found
    Capture Page Screenshot    boardeditdialog.png
    Click Element    //button[@class='btn-primary']

Open Board
    [Arguments]    ${min}=2    ${max}=4
    Get Board Xpath    ${min}    ${max}
    Click Element    ${Bxpath}
    Wait Until Page Contains Element    //div[@class='board']    timeout=2    error=Board Not Open
    Capture Page Screenshot    boardview.png

Close Board
    Click Element    //span[@class='fa fa-fw fa-arrow-left']
    Wait Until Page Contains Element    //div[@class='content']    timeout=2    error=Board list Not Found
    Location Should Be    ${BoardUrl}

Create Ticket
    [Arguments]    ${min}    ${max}
    Get X Coordinate
    Get Y Coordinate
    Double Click Element At Coordinates    //div[@class='board']    ${x}    ${y}
    Wait Until Page Contains Element    //div[@class="ticket"]    timeout=2    error=Ticket Not Created
    Move Ticket    ${min}    ${max}    ${x}    ${y}

Move Ticket
    [Arguments]    ${min}    ${max}    ${x}    ${y}
    Get Ticket Xpath    ${min}    ${max}
    Get Move Coordinates    ${x}    ${y}
    Drag And Drop By Offset    ${Txpath}    ${xMove}    ${yMove}

Opent Ticket Edit Dialog
    [Arguments]    ${min}=2    ${max}=5
    Get Ticket Xpath    ${min}    ${max}
    Double Click Element    ${Txpath}
    Wait Until Page Contains Element    //form[@class='dialog edit-ticket-dialog']    timeout=2    error=Edit Ticket Dialog Not Found
    Capture Page Screenshot    ticketeditdialog.png
    ${status}    ${editdialog} =    Run Keyword And Ignore Error    Check Ticket Edit Dialog
    Run Keyword If    '${status}'=='PASS'    Input to Empty Ticket
    Run Keyword Unless    '${status}'=='PASS'    Input to Edited Ticket

Check Ticket Edit Dialog
    Page Should Contain Element    //input[@placeholder='Ticket header']

Input to Empty Ticket
    Input Text    //input[@placeholder='Ticket header']    Ticket Header
    ${rid}    Get Element Attribute    //form[@class='dialog edit-ticket-dialog']@data-reactid
    Input Text    //input[@class='comment-input']    Ticket comment
    Click Button    //button[@data-reactid='${rid}.1.4.0.1']
    Input Text    //textarea[@placeholder='Ticket content']    Ticket text...
    Capture Page Screenshot    editedticket.png
    Click Button    //button[@id='ticket-dialog-save']

Input to Edited Ticket
    ${rid}    Get Element Attribute    //form[@class='dialog edit-ticket-dialog']@data-reactid
    Click Element    //span[@data-reactid='${rid}.1.0.0']
    Page Should Contain Element    //input[@placeholder='Ticket header']
    Input Text    //input[@placeholder='Ticket header']    Ticket Header
    ${rid}    Get Element Attribute    //form[@class='dialog edit-ticket-dialog']@data-reactid
    Input Text    //input[@class='comment-input']    Ticket comment
    Click Button    //button[@data-reactid='${rid}.1.4.0.1']
    Input Text    //textarea[@placeholder='Ticket content']    Ticket text...
    Capture Page Screenshot    editedticket.png
    Click Button    //button[@id='ticket-dialog-save']

Create Ticket with Markdown Text
    Get X Coordinate
    Get Y Coordinate
    Double Click Element At Coordinates    //div[@class='board']    ${x}    ${y}
    Wait Until Page Contains Element    //div[@class="ticket"]    timeout=2    error=Ticket Not Created
    Get Ticket Xpath    6    6
    Double Click Element    ${Txpath}
    Wait Until Page Contains Element    //form[@class='dialog edit-ticket-dialog']    timeout=2    error=Edit Ticket Dialog Not Found
    Input Markdown Text
    Capture Page Screenshot    inputmarkdown.png
    Click Button    //button[@id='ticket-dialog-save']
    Double Click Element    ${Txpath}
    Capture Page Screenshot    markdownticket.png
    Click Button    //button[@id='ticket-dialog-save']

Input Markdown Text
    Get Markdown Text From File    //textarea[@placeholder='Ticket content']

Open Share Dialog
    Click Element    //span[@class='fa fa-fw fa-share-alt']
    Wait Until Page Contains Element    //form[@class='dialog dialog-edit-board']    timeout=2    error=Board Edit Dialog Not Found
    Capture Page Screenshot    sharedialog.png
    Click Button    //button[@class='btn-primary']

Open Export Dialog
    Click Element    //span[@class='fa fa-fw fa-download']
    Wait Until Page Contains Element    //form[@class='dialog dialog-edit-board']    timeout=2    error=Board Edit Dialog Not Found
    Capture Page Screenshot    exportdialog.png
    Click Element    //button[@class='btn-primary']

Open Help
    Click Element    //span[@class='fa fa-fw fa-question']
    Wait Until Element Is Visible    //div[@class='infospace']    timeout=2    error=Help not open
    Capture Page Screenshot    helpscreen.png

Close Help
    Click Element    //span[@class='fa fa-fw fa-times']
    Element Should Not Be Visible    //div[@class='infospace']

Open Board Members
    Click Element    //span[@class='fa fa-fw fa-users']
    Wait Until Page Contains Element    //form[@class='dialog dialog-board-members']
    Capture Page Screenshot    boardmembers.png

Close Board Members
    Click Button    //button[@class='btn-primary']

Open Profile
    Click Element    //div[@class='avatar online']
    Wait Until Page Contains Element    //ul[@class='dropdown options']    timeout=2    error=Dropdown List Not Found
    Click Element    //span[@class='fa fa-fw fa-user']
    Wait Until Page Contains Element    //form[@class='login-info']
    Capture Page Screenshot    profiledialog.png

Close Profile
    Click Element    //span[@class='fa fa-times']
    Element Should Not Be Visible    //form[@class='login-info']

Open About
    Click Element    //div[@class='avatar online']
    Wait Until Page Contains Element    //ul[@class='dropdown options']    timeout=2    error=Dropdown List Not Found
    Click Element    //span[@class='fa fa-fw fa-info']
    Wait Until Page Contains Element    //form[@class='dialog dialog-about']    timeout=2    error=About Dialog Not Open
    Capture Page Screenshot    aboutdialog.png

Close About
    Click Button    //button[@class='btn-primary']
    Element Should Not Be Visible    //form[@class='dialog dialog-about']
    Click Element    //div[@class='avatar online']
    Element Should Not Be Visible    //ul[@class='dropdown options']

Review Tickets
    Click Element    //span[@class='fa fa-fw fa-eye']
    Wait Until Page Contains Element    //div[@class='review-dialog-container']
    Capture Page Screenshot    ticketreview.png
    Click Element    //span[@class='fa fa-chevron-right']
    Click Element    //span[@class='fa fa-chevron-left']
    Click Element    //span[@class="fa fa-fw fa-arrow-left board-link"]

Open Feedback
    Click Element    //div[@class='avatar online']
    Wait Until Element Is Visible    //ul[@class='dropdown options']    timeout=2    error=Dropdown List Not Found
    Click Element    //div[@class='user-voice-trigger']
    Select Frame    //iframe[@class='uvw-dialog-iframe uv-popover-iframe']
    Wait Until Element Is Visible    //article[@class='widget large-widget']    timeout=2    error=Widget Not Open
    Element Text Should Be    //h1[@class='pane-title']    Send us a message    error=Write message page not open
    Capture Page Screenshot    feedbackdialog.png
    Click Element    //i[@class='icon close-icon']
    Element Should Not Be Visible    //article[@class='widget large-widget']
    Unselect Frame
    Click Element    //div[@class='avatar online']
    Element Should Not Be Visible    //ul[@class='dropdown options']

Open Localization Dialog
    Click Element    //div[@class='avatar online']
    Wait Until Page Contains Element    //ul[@class='dropdown options']    timeout=2    error=Dropdown List Not Found
    Click Element    //li[@id='options-language']
    Wait Until Page Contains Element    //ul[@class='dropdown locales']    timeout=2    error=Langueage List not found
    Capture Page Screenshot    localization.png
    Click Element    //div[@class='avatar online']
    Element Should Not Be Visible    //ul[@class='dropdown options']

Click Magnet On
    Assign Id To Element    //div[@class='view view-board']    reactID
    ${rid}    Get Element Attribute    reactID@data-reactid
    Click Element    //span[@class='fa fa-fw fa-magnet']
    Assign Id To Element    //div[@data-reactid='${rid}.5.$magnet']    magnetOnID
    ${class}    Get Element Attribute    magnetOnID@class
    Should Match    ${class}    control active
    Capture Page Screenshot    snaptogrid.png

Click Magnet Off
    Assign Id To Element    //div[@class='view view-board']    reactID
    ${rid}    Get Element Attribute    reactID@data-reactid
    Click Element    //span[@class='fa fa-fw fa-magnet']
    Assign Id To Element    //div[@data-reactid='${rid}.5.$magnet']    magnetOffID
    ${class}    Get Element Attribute    magnetOffID@class
    Should Match    ${class}    control

Click Globe On
    Assign Id To Element    //div[@class='view view-board']    reactID
    ${rid}    Get Element Attribute    reactID@data-reactid
    Element Should Not Be Visible    //div[@data-reactid='${rid}.2.0.1']
    Click Element    //span[@class='fa fa-fw fa-globe']
    Assign Id To Element    //div[@data-reactid='${rid}.5.$globe']    globeOnID
    ${class}    Get Element Attribute    globeOnID@class
    Should Match    ${class}    control active
    Element Should Be Visible    //div[@data-reactid='${rid}.2.0.1']
    Capture Page Screenshot    minimap.png

Click Globe Off
    Assign Id To Element    //div[@class='view view-board']    reactID
    ${rid}    Get Element Attribute    reactID@data-reactid
    Element Should Be Visible    //div[@data-reactid='${rid}.2.0.1']
    Click Element    //span[@class='fa fa-fw fa-globe']
    Assign Id To Element    //div[@data-reactid='${rid}.5.$globe']    globeOffID
    ${class}    Get Element Attribute    globeOffID@class
    Should Match    ${class}    control
    Element Should Not Be Visible    //div[@data-reactid='${rid}.2.0.1']

Log Out
    Click Element    //div[@class='avatar online']
    Wait Until Page Contains Element    //ul[@class='dropdown options']    timeout=2    error=Dropdown List Not Found
    Capture Page Screenshot    settingsdropwdown.png
    Click Element    //li[@id='options-sign-out']
    Wait Until Page Contains Element    //form[@class='form']    timeout=2    error=Login Form not found
    Location Should Be    ${LoginUrl}

Get Board Xpath
    [Arguments]    ${min}    ${max}
    ${int}    Evaluate    random.randint(${min}, ${max})    random
    Set Test Variable    ${Bxpath}    xpath=/html/body/div/div/div[2]/div[${int}]/div[1]/div

Get Edit Xpath
    [Arguments]    ${min}    ${max}
    ${int}    Evaluate    random.randint(${min}, ${max})    random
    Set Test Variable    ${Expath}    xpath=/html/body/div/div/div[2]/div[${int}]/div[3]/div[2]/span

Get Ticket Xpath
    [Arguments]    ${min}    ${max}
    ${int}    Evaluate    random.randint(${min}, ${max})    random
    Set Test Variable    ${Txpath}    xpath=/html/body/div/div/div[2]/div/div[1]/div[${int}]
    Set Test Variable    \${tint}    ${EMPTY}
    Set Test Variable    ${tint}    ${int}

Get Y Coordinate
    [Arguments]    ${min}=-540    ${max}=540
    ${int}    Evaluate    random.randint(${min}, ${max})    random
    Set Test Variable    ${y}    ${int}

Get X Coordinate
    [Arguments]    ${min}=-960    ${max}=960
    ${int}    Evaluate    random.randint(${min}, ${max})    random
    Set Test Variable    ${x}    ${int}

Get Move Coordinates
    [Arguments]    ${x}    ${y}
    Run Keyword If    '${x}'<'0'    Calculate X Negative    ${x}
    Run Keyword If    '${x}'>'0'    Calculate X Positive    ${x}
    Run Keyword If    '${x}'=='0'    Calculate X Zero    ${x}
    Run Keyword If    '${y}'<'0'    Calculate Y Negative    ${y}
    Run Keyword If    '${y}'>'0'    Calculate Y Positive    ${y}
    Run Keyword If    '${y}'=='0'    Calculate Y Zero    ${y}

Calculate X Negative
    [Arguments]    ${x}
    ${minX}    Evaluate    -${x}-960
    ${maxX}    Evaluate    ${minX}+1920
    Get X Move    ${minX}    ${maxX}

Calculate X Positive
    [Arguments]    ${x}
    ${maxX}    Evaluate    960-${x}
    ${minX}    Evaluate    ${maxX}-1920
    Get X Move    ${minX}    ${maxX}

Calculate X Zero
    [Arguments]    ${x}
    ${maxX}    Evaluate    ${x}+960
    ${minX}    Evaluate    ${x}-960
    Get X Move    ${minX}    ${maxX}

Calculate Y Negative
    [Arguments]    ${y}
    ${minY}    Evaluate    -${y}-540
    ${maxY}    Evaluate    ${minY}+1080
    Get Y Move    ${minY}    ${maxY}

Calculate Y Positive
    [Arguments]    ${y}
    ${maxY}    Evaluate    540-${y}
    ${minY}    Evaluate    ${maxY}-1080
    Get Y Move    ${minY}    ${maxY}

Calculate Y Zero
    [Arguments]    ${y}
    ${maxY}    Evaluate    ${y}+540
    ${minY}    Evaluate    ${y}-540
    Get Y Move    ${minY}    ${maxY}

Get X Move
    [Arguments]    ${min}=-960    ${max}=960
    ${int}    Evaluate    random.randint(${min}, ${max})    random
    Set Test Variable    ${xMove}    ${int}

Get Y Move
    [Arguments]    ${min}=-540    ${max}=540
    ${int}    Evaluate    random.randint(${min}, ${max})    random
    Set Test Variable    ${yMove}    ${int}
