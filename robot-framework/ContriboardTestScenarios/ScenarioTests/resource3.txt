*** Settings ***
Library           ExtendedSelenium2Library
Library           Collections

*** Variables ***
${Browser}        firefox
#{Browser}        ie
${DELAY}          0.1
${ValidUser}      evelyn.holmes@test.com
${ValidPassword}    evelynholmes
${InvalidUser}    invalid@test.com
${InvalidPassword}    Invalid1
${SERVER}         lankku.n4s.jamk.org
${MainUrl}        http://${SERVER}
${LoginUrl}       http://${SERVER}/login
${BoardUrl}       http://${SERVER}/boards
${RegUrl}         http://${SERVER}/register
${LoginTitle}     Contriboard

*** Keywords ***
Login User
    Input Username    ${ValidUser}
    Input User Password    ${ValidPassword}
    ${check}    Get Element Attribute    //input[@class='btn-primary']@value
    Should Be Equal    ${check}    Login    error=Login button missing
    Submit Credentials
    Wait Until Page Contains Element    //div[@class='view view-workspace']    timeout=2    error=Board list Not Found
    Location Should Be    ${BoardUrl}

Click Profile
    Click Element    //span[@class='fa fa-fw fa-user']
    Wait Until Element Is Visible    //ul[@class='dropdown']    timeout=2    error=Dropdown List Not Found

Click Feedback
    Click Element    //div[@class='user-voice-trigger']
    Select Frame    //iframe[@class='uvw-dialog-iframe uv-popover-iframe']
    Wait Until Element Is Visible    //article[@class='widget large-widget']    timeout=2    error=Widget Not Open
    Element Text Should Be    //h1[@class='pane-title']    Send us a message    error=Write message page not open
    Click Element    //i[@class='icon close-icon']
    Element Should Not Be Visible    //article[@class='widget large-widget']    error=Widget Not Closed
    Unselect Frame

Close Profile
    Click Element    //span[@class='fa fa-fw fa-user']
    Element Should Not Be Visible    //ul[@class='dropdown']    error=Dropdown not closed

Create Board
    Click Element    //span[@class='fa fa-fw fa-plus']

Click Edit Board
    [Arguments]    ${min}=2    ${max}=6
    Get Edit Xpath    ${min}    ${max}
    Click Element    ${Expath}
    Wait Until Page Contains Element    //form[@class='dialog dialog-edit-board']    timeout=2    error=Board Edit Dialog Not Found

Click Done Board Edit
    Click Button    //button[@class='btn-primary']
    Element Should Not Be Visible    //form[@class='dialog dialog-edit-board']    error=Edit Not Closed

Click Done Ticket Edit
    Click Button    //button[@class='btn-primary']
    Element Should Not Be Visible    //form[@class='dialog edit-ticket-dialog']    error=Edit Not Closed

Open Board
    [Arguments]    ${min}=2    ${max}=6
    Get Board Xpath    ${min}    ${max}
    Click Element    ${Bxpath}
    Wait Until Page Contains Element    //div[@class='board']    timeout=2    error=Board Not Open

Close Board
    Click Element    //span[@class='fa fa-fw fa-arrow-left']
    Wait Until Page Contains Element    //div[@class='content']    timeout=2    error=Board list Not Found
    Location Should Be    ${BoardUrl}

Delete Board
    [Arguments]    ${min}=2    ${max}=6
    Get Delete Xpath    ${min}    ${max}
    Assign Id To Element    ${Dxpath}    Board
    Click Element    ${Dxpath}
    Wait Until Page Contains Element    //form[@class='dialog dialog-remove-board']    timeout=2    error=Board Edit Dialog Not Found
    Click Button    //button[@class='btn-danger']
    Element Should Not Be Visible    Board    error=Board Not Deleted

Create Ticket
    Get X Coordinate
    Get Y Coordinate
    Double Click Element At Coordinates    //div[@class='board']    ${x}    ${y}
    Wait Until Page Contains Element    //div[@class="ticket"]    timeout=2    error=Ticket Not Created

Move Ticket
    [Arguments]    ${min}=1    ${max}=5
    Get Ticket Xpath    ${min}    ${max}
    Get X Move
    Get Y Move
    Drag And Drop By Offset    ${Txpath}    ${xMove}    ${yMove}

Edit Ticket
    [Arguments]    ${min}=1    ${max}=5
    Get Ticket Xpath    ${min}    ${max}
    Double Click Element    ${Txpath}
    Wait Until Page Contains Element    //form[@class='dialog edit-ticket-dialog']    timeout=2    error=Edit Ticket Dialog Not Found
    ${rid}    Get Element Attribute    //form[@class='dialog edit-ticket-dialog']@data-reactid
    Input Text    //textarea[@data-reactid='${rid}.1.0']    Typing something
    Click Button    //button[@class='btn-primary']

Delete Ticket
    [Arguments]    ${min}=1    ${max}=5
    Get Ticket Xpath    ${min}    ${max}
    Assign Id To Element    ${Txpath}    Ticket
    Double Click Element    ${Txpath}
    Wait Until Page Contains Element    //form[@class='dialog edit-ticket-dialog']    timeout=2    error=Edit Ticket Dialog Not Found
    Click Button    //button[@class='btn-danger']
    Element Should Not Be Visible    ticket    error=Ticket Not Deleted

Click Edit Board From Board
    Wait Until Page Contains Element    //div[@class='controls']    timeout=2    error=Controls Not Found
    Click Element    //span[@class='fa fa-fw fa-pencil']
    Wait Until Page Contains Element    //form[@class='dialog dialog-edit-board']

Open Ticket Edit
    [Arguments]    ${min}=1    ${max}=5
    Get Ticket Xpath    ${min}    ${max}
    Double Click Element    ${Txpath}
    Wait Until Page Contains Element    //form[@class='dialog edit-ticket-dialog']    timeout=2    error=Edit Ticket Dialog Not Found

Input Ticket Text
    [Arguments]    ${Ttext}=Typing Something
    Wait Until Page Contains Element    //form[@class='dialog edit-ticket-dialog']    timeout=2    error=Edit Ticket Dialog Not Found
    ${rid}    Get Element Attribute    //form[@class='dialog edit-ticket-dialog']@data-reactid
    Input Text    //textarea[@data-reactid='${rid}.1.0']    ${Ttext}

Click Magnet On
    Assign Id To Element    //div[@class='view view-board']    reactID
    ${rid}    Get Element Attribute    reactID@data-reactid
    Click Element    //span[@class='fa fa-fw fa-magnet']
    Assign Id To Element    //div[@data-reactid='${rid}.4.$magnet']    magnetOnID
    ${class}    Get Element Attribute    magnetOnID@class
    Should Match    ${class}    control active

Click Magnet Off
    Assign Id To Element    //div[@class='view view-board']    reactID
    ${rid}    Get Element Attribute    reactID@data-reactid
    Click Element    //span[@class='fa fa-fw fa-magnet']
    Assign Id To Element    //div[@data-reactid='${rid}.4.$magnet']    magnetOffID
    ${class}    Get Element Attribute    magnetOffID@class
    Should Match    ${class}    control

Click Globe On
    Assign Id To Element    //div[@class='view view-board']    reactID
    ${rid}    Get Element Attribute    reactID@data-reactid
    #Element Should Not Be Visible    //div[@data-reactid='${rid}.1.0.1']
    Click Element    //span[@class='fa fa-fw fa-globe']
    Assign Id To Element    //div[@data-reactid='${rid}.4.$globe']    globeOnID
    ${class}    Get Element Attribute    globeOnID@class
    Should Match    ${class}    control active
    #Element Should Be Visible    //div[@data-reactid='${rid}.1.0.1']

Click Globe Off
    Assign Id To Element    //div[@class='view view-board']    reactID
    ${rid}    Get Element Attribute    reactID@data-reactid
    #Element Should Be Visible    //div[@data-reactid='${rid}.1.0.1']
    Click Element    //span[@class='fa fa-fw fa-globe']
    Assign Id To Element    //div[@data-reactid='${rid}.4.$globe']    globeOffID
    ${class}    Get Element Attribute    globeOffID@class
    Should Match    ${class}    control
    #Element Should Not Be Visible    //div[@data-reactid='${rid}.1.0.1']

Log Out
    Click Element    //span[@class='fa fa-fw fa-user']
    Wait Until Page Contains Element    //ul[@class='dropdown']    timeout=2    error=Dropdown List Not Found
    Assign Id To Element    //ul[@class='dropdown']    Dropdown
    ${did}    Get Element Attribute    Dropdown@data-reactid
    Click Element    //li[@data-reactid='${did}.$3']
    Wait Until Page Contains Element    //form[@class='form']    timeout=2    error=Login Form not found
    Location Should Be    ${LoginUrl}

Input Username
    [Arguments]    ${username}
    Input Text    //input[@type='email']    ${username}

Input User Password
    [Arguments]    ${password}
    Input Password    //input[@type='password']    ${password}

Submit Credentials
    Click Button    //input[@class='btn-primary']

Invalid Login
    [Arguments]    ${username}    ${password}
    Input Username    ${username}
    Input User Password    ${password}
    Submit Credentials
    Login Should Have Failed

Login Should Have Failed
    Location Should Be    ${LoginUrl}
    #Temporary fix for new login alerts
    Element Should Not Be Visible    //div[@class='view view-workspace']    error=Login Succeeded

Open Browser To Login Page
    Open Browser    ${LoginUrl}    ${Browser}
    Maximize Browser Window
    Set Selenium Speed    ${DELAY}
    Location Should Be    ${LoginUrl}
    Title Should Be    ${LoginTitle}
    Wait Until Page Contains Element    //form[@class='form']    timeout=2    error=Login Form Not Found

Go To Login Page
    Go To    ${LoginUrl}
    Location Should Be    ${LoginUrl}

Get Board Xpath
    [Arguments]    ${min}    ${max}
    ${int}    Evaluate    random.randint(${min}, ${max})    random
    Set Test Variable    ${Bxpath}    xpath=/html/body/div/div/div[2]/div[${int}]/div[1]/div

Get Edit Xpath
    [Arguments]    ${min}    ${max}
    ${int}    Evaluate    random.randint(${min}, ${max})    random
    Set Test Variable    ${Expath}    xpath=/html/body/div/div/div[2]/div[${int}]/div[3]/div[2]/span

Get Delete Xpath
    [Arguments]    ${min}    ${max}
    ${int}    Evaluate    random.randint(${min}, ${max})    random
    Set Test Variable    ${Dxpath}    xpath=/html/body/div/div/div[2]/div[${int}]/div[3]/div[1]/span

Get Ticket Xpath
    [Arguments]    ${min}    ${max}
    ${int}    Evaluate    random.randint(${min}, ${max})    random
    Set Test Variable    ${Txpath}    xpath=/html/body/div/div/div[2]/div/div[1]/div[${int}]

Get X Move
    [Arguments]    ${min}=-450    ${max}=450
    ${int}    Evaluate    random.randint(${min}, ${max})    random
    Set Test Variable    ${xMove}    ${int}

Get Y Move
    [Arguments]    ${min}=-215    ${max}=215
    ${int}    Evaluate    random.randint(${min}, ${max})    random
    Set Test Variable    ${yMove}    ${int}

Get Y Coordinate
    [Arguments]    ${min}=-215    ${max}=215
    ${int}    Evaluate    random.randint(${min}, ${max})    random
    Set Test Variable    ${y}    ${int}

Get X Coordinate
    [Arguments]    ${min}=-350    ${max}=350
    ${int}    Evaluate    random.randint(${min}, ${max})    random
    Set Test Variable    ${x}    ${int}
