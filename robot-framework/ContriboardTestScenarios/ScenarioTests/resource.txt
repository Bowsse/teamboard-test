*** Settings ***
Library           ExtendedSelenium2Library
Library           Collections

*** Variables ***
${Browser}        firefox
${DELAY}          0.1
${SERVER}         localhost:8000
${MainUrl}        http://${SERVER}
${LoginUrl}       http://${SERVER}/login
${BoardUrl}       http://${SERVER}/boards
${RegUrl}         http://${SERVER}/register
${LoginTitle}     Contriboard

*** Keywords ***
Open Browser To Login Page
    Open Browser    ${LoginUrl}    ${Browser}
    Maximize Browser Window
    Set Selenium Speed    ${DELAY}
    Location Should Be    ${LoginUrl}
    Title Should Be    ${LoginTitle}
    Wait Until Page Contains Element    //form[@class='form']    timeout=2    error=Login Form Not Found

Register User
    [Arguments]    ${NewUser}    ${NewPassword}
    Element Text Should Be    //button[@class='btn-secondary']    Register    error=Register button missing
    Click Button    //button[@class='btn-secondary']
    Wait Until Page Contains Element    //form[@class='form']    timeout=2    error=Form not found
    Location Should Be    ${RegUrl}
    Input Text    //input[@type='email']    ${NewUser}
    Input Password    //input[@type='password']    ${NewPassword}
    ${check}    Get Element Attribute    //input[@class='btn-primary']@value
    Should Be Equal    ${check}    Register    error=Register button missing
    Click Button    //input[@class='btn-primary']
    Wait Until Page Contains Element    //div[@class='view view-workspace']    timeout=2    error=Board list Not Found
    Wait Until Page Contains Element    //div[@class='item broadcast']    timeout=2    error=Welcome Not Found
    Location Should Be    ${BoardUrl}
    Click Element    //div[@class='item broadcast']

Log Out
    Click Element    //span[@class='fa fa-fw fa-user']
    Wait Until Page Contains Element    //ul[@class='dropdown']    timeout=2    error=Dropdown List Not Found
    Assign Id To Element    //ul[@class='dropdown']    Dropdown
    ${did}    Get Element Attribute    Dropdown@data-reactid
    Click Element    //li[@data-reactid='${did}.$3']
    Wait Until Page Contains Element    //form[@class='form']    timeout=2    error=Login Form not found
    Location Should Be    ${LoginUrl}

Create Board
    Click Element    //span[@class='fa fa-fw fa-plus']

Click Edit Board
    [Arguments]    ${min}=2    ${max}=6
    Get Edit Xpath    ${min}    ${max}
    Click Element    ${Expath}
    Wait Until Page Contains Element    //div[@class='dialog dialog-edit-board']    timeout=2    error=Board Edit Dialog Not Found

Input Board Name
    Input Text    //input[@placeholder='Board Name']    Birthday Party!

Click Done Board Edit
    Click Button    //button[@class='btn-primary']
    Element Should Not Be Visible    //div[@class='dialog dialog-edit-board']    error=Edit Not Closed

Change Background
    [Arguments]    ${min}=0    ${max}=7
    Get Random Background    ${min}    ${max}
    Assign Id To Element    //div[@class='dialog-overlay']    Content
    ${rid}    Get Element Attribute    Content@data-reactid
    Select From List By Value    //select[@data-reactid='${rid}.0.1.4.2.0']    ${background}
    Run Keyword If    '${background}'=='NONE'    Empty Background
    Run Keyword Unless    '${background}'=='NONE'    Check Background

Empty Background
    ${check}    Get Element Attribute    //div[@data-reactid='${rid}.0.1.4.0.0']@class
    Should Be Equal    ${check}    blanko    error=Background Not Empty

Check Background
    ${check}    Get Element Attribute    //img[@data-reactid='${rid}.0.1.4.0.0']@src
    Should Be Equal    ${check}    ${MainUrl}/dist/assets/img/bg/${bcheck}.png    error=Background Not Changed

Get Edit Xpath
    [Arguments]    ${min}    ${max}
    ${int}    Evaluate    random.randint(${min}, ${max})    random
    Set Test Variable    ${Expath}    xpath=/html/body/div/div/div[2]/div[${int}]/div[3]/div[2]/span

Get Random Background
    [Arguments]    ${min}    ${max}
    ${int}    Evaluate    random.randint(${min}, ${max})    random
    ${list}    Create List    NONE    PLAY    SWOT    SCRUM    KANBAN
    ...    KEEP_DROP_TRY    CUSTOMER_JOURNEY_MAP    BUSINESS_MODEL_CANVAS
    ${item}    Get From List    ${list}    ${int}
    Set Test Variable    ${background}    ${item}
    ${list2}    Create List    blank    play    swot    scrum    kanban
    ...    keep_drop_try    customer_journey_map    business_model_canvas
    ${item2}    Get From List    ${list2}    ${int}
    Set Test Variable    ${bcheck}    ${item2}
